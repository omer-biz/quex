WHITESPACE = _{ " " }

description = @{ (!NEWLINE ~ ANY)+ }

gregorian_month = {
    "jan"
  | "feb"
  | "mar"
  | "apr"
  | "may"
  | "jun"
  | "jul"
  | "aug"
  | "sep"
  | "oct"
  | "nov"
  | "dec"
}
ethiopian_month = {
    "mes"
  | "መስከ"
  | "tik"
  | "ጥቅም"
  | "hed"
  | "ህዳር"
  | "tah"
  | "ታኅሣ"
  | "tir"
  | "ጥር"
  | "yek"
  | "የካቲ"
  | "meg"
  | "መጋቢ"
  | "miy"
  | "ሚያዝ"
  | "gin"
  | "ግንቦ"
  | "sen"
  | "ሴኒ"
  | "ham"
  | "ሐምሌ"
  | "neh"
  | "ነሐሴ"
  | "pua"
  | "ጳጉሜ"
}

day            = ${ ASCII_DIGIT{1, 2} }
year           = { ASCII_DIGIT{1, 4} }
named_yearly   = { ASCII_DIGIT{1, 4} ~ "*" }
yearly         = { "*" }

gregorian_date = { (yearly | named_yearly | year) ~ gregorian_month ~ day }
ethiopian_date = { (yearly | named_yearly | year) ~ ethiopian_month ~ day }

recurring_monthly = { "d=" ~ ASCII_DIGIT{1, 2} }

clock = { ASCII_DIGIT{1, 2} ~ ":" ~ ASCII_DIGIT{1, 2} ~ ( ":" ~ ASCII_DIGIT{1, 2} )? }
am_pm = { clock ~ (^"am" | ^"pm") }
time  = { am_pm | clock }

schedule = {
    (recurring_monthly | gregorian_date | ethiopian_date) ~ time? ~ "," ~ description ~ NEWLINE*
}

schedule_list = _{ SOI ~ schedule* ~ EOI}
